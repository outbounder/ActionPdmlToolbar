<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
					   xmlns:s="library://ns.adobe.com/flex/spark" 
					   xmlns:mx="library://ns.adobe.com/flex/halo"
					   xmlns:pdml="components.pdml.*"
					   width="100%"
					   height="100%"
					   currentState="loading"
					   preinitialize="onPreinitialize()"
					   creationComplete="onCreationComplete()"
					   applicationComplete="onAppComplete()"
					   mouseDown="onMouseDown(event)" xmlns:components="components.*" xmlns:modules="components.modules.*">
	
	<fx:Style>
		
		@namespace "library://ns.adobe.com/flex/spark";
		WindowedApplication 
		{
			skinClass: ClassReference("skins.AppSkin");
		}
		
	</fx:Style>
	
	<fx:Script>
		<![CDATA[
			import data.model.MultiModuleInfo;
			import data.model.PdmlFile;
			
			import flash.Boot;
			import flash.events.DataEvent;
			import flash.events.InvokeEvent;
			import flash.events.KeyboardEvent;
			import flash.events.MouseEvent;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.managers.DragManager;
			
			import org.dassista.api.rest.as3.RestService;
			
			[Bindable]
			private var pdmlStack:ArrayCollection;
			[Bindable]
			private var modulesList:ArrayCollection;
			[Bindable]
			private var currentPdml:PdmlFile;
			
			private var oldWidth:int;
			private var oldHeight:int;
			
			private function onPreinitialize():void
			{
				var boot:Boot = new Boot(this.stage as MovieClip); // haxe boot
			}
			
			private function onCreationComplete():void
			{
				this.alwaysInFront = true;
				this.snapToEdge();
				
				this.addEventListener(NativeDragEvent.NATIVE_DRAG_ENTER, onNativeDragEnter);
				this.addEventListener(NativeDragEvent.NATIVE_DRAG_DROP, onNativeDragDrop);
				
				this.pdmlStack = new ArrayCollection();
			}
			
			private function onAppComplete():void
			{
				this.stage.addEventListener(KeyboardEvent.KEY_DOWN, onKeyDown, true);
				
				var stackList:RestService = new RestService();
				stackList.setValue("moduleName", "org.dassista.modules.RepoInfo");
				stackList.setValue("methodName", "getPdmlStackList");
				stackList.load(onStackListResult);
				cursorManager.setBusyCursor();
			}
			
			private function onStackListResult(e:DataEvent):void
			{
				cursorManager.removeBusyCursor();
				
				if(e.data.indexOf("ioerror") != -1)
				{
					Alert.show(e.data);
					return;
				}
				var dataXML:XML = XML("<result>"+e.data+"</result>");
				for each(var output:XML in dataXML.output)
					this.pdmlStack.addItem(new PdmlFile(output.text()));
				
				// module info
				var moduleInfoContext:RestService = new RestService();
				moduleInfoContext.setValue("moduleName", "org.dassista.modules.ModuleInfo");
				moduleInfoContext.setValue("methodName", "execute");
				moduleInfoContext.setValue("target", "_app.org.dassista.tools");
				
				moduleInfoContext.load(onModuleInfoResult);
			}
			
			private function onModuleInfoResult(e:DataEvent):void
			{
				this.modulesList = new ArrayCollection();
				
				var result:XML = XML("<result>"+e.data+"</result>");
				for each(var node:XML in result.children())
				this.modulesList.addItem(new MultiModuleInfo(node));
				
				this.currentState = "normal";
				this.addEventListener(InvokeEvent.INVOKE, onInvoke);
			}
			
			private function onInvoke(e:InvokeEvent):void
			{
				if(e.arguments.length == 0)
					return;
				
				if(e.arguments.length == 1)
				{
					var arg:String = e.arguments[0] as String;
					if(arg.indexOf(".pdml") != -1)
					{
						// invoke to execute ;)
						this.addPdmlFile(arg);
					}
					else if(arg == "showhide")
					{
						if(this.currentState == "normal")
							this.hide();
						else
							this.show();
					}
					else
						this.show();
				}
			}
			
			private function onKeyDown(e:KeyboardEvent):void
			{
				if(e.keyCode == Keyboard.ESCAPE)
					this.hide();
			}

			private function onMouseDown(event:MouseEvent):void
			{
				if(event.type == MouseEvent.MOUSE_DOWN && event.controlKey)
					this.nativeWindow.startMove();
			}
			
			private function onNativeDragEnter(e:NativeDragEvent):void
			{
				if(e.clipboard.hasFormat('air:file list'))
				{
					var fileList:Array = e.clipboard.getData('air:file list') as Array;
					if(fileList.length == 1)
						DragManager.acceptDragDrop(this);
				}
			}
			
			private function onNativeDragDrop(e:NativeDragEvent):void
			{
				if(e.clipboard.hasFormat('air:file list'))
				{
					var fileList:Array = e.clipboard.getData('air:file list') as Array;
					var draggedFile:File = fileList[0] as File;
					this.addPdmlFile(draggedFile.nativePath);
				}
			}
			
			private function addPdmlFile(filePath:String):void
			{
				var getClassPath:RestService = new RestService();
				getClassPath.setValue("moduleName", "org.dassista.modules.RepoInfo");
				getClassPath.setValue("methodName", "stackPdmlFile");
				getClassPath.setValue("target", filePath);
				getClassPath.load(onGetClassPathResult);
				cursorManager.setBusyCursor();
			}
			
			private function onGetClassPathResult(e:DataEvent):void
			{
				cursorManager.removeBusyCursor();
				
				if(e.data.indexOf("ioerror") != -1)
				{
					Alert.show(e.data);
					return;
				}
				var dataXML:XML = XML(e.data);
				
				this.pdmlStack.addItem(new PdmlFile(dataXML.text()));
			}
			
			private function hide(event:MouseEvent = null):void
			{
				if(event != null && event.controlKey)
					return;
				
				this.oldWidth = this.nativeWindow.width;
				this.oldHeight = this.nativeWindow.height;
				this.currentState = "hidden";
				this.nativeWindow.width = 40;
				this.nativeWindow.height = 20;
				this.snapToEdge();
			}
			
			private function show(event:MouseEvent = null):void
			{
				if(event != null && event.controlKey)
					return;
				
				this.currentState = "normal";
				this.nativeWindow.width = oldWidth;
				this.nativeWindow.height = oldHeight;
				this.ensureVisible();
			}
			
			private function ensureVisible():void
			{
				if(this.nativeWindow.x+this.nativeWindow.width > Screen.mainScreen.visibleBounds.width)
					this.nativeWindow.x = Screen.mainScreen.visibleBounds.width-this.nativeWindow.width;
				if(this.nativeWindow.y+this.nativeWindow.height > Screen.mainScreen.visibleBounds.height)
					this.nativeWindow.y = Screen.mainScreen.visibleBounds.height-this.nativeWindow.height;
			}

			private function snapToEdge():void
			{
				if(this.nativeWindow.x+this.nativeWindow.width/2 > Screen.mainScreen.visibleBounds.width/2)
					this.nativeWindow.x = Screen.mainScreen.visibleBounds.width-this.nativeWindow.width;
				else
					this.nativeWindow.x = 0;
				if(this.nativeWindow.y+this.nativeWindow.height/2 > Screen.mainScreen.visibleBounds.height/2)
					this.nativeWindow.y = Screen.mainScreen.visibleBounds.height-this.nativeWindow.height;
				else
					this.nativeWindow.y = 0;
			}

		]]>
	</fx:Script>
	
	<s:states>
		<s:State name="loading" />
		<s:State name="normal" />
		<s:State name="hidden" />
	</s:states>
	
	<s:Rect width="100%" height="100%">
		<s:fill>
			<s:SolidColor color="gray" alpha="0.5" />
		</s:fill>
		<s:stroke>
			<s:SolidColorStroke color="black" weight="1" />
		</s:stroke>
	</s:Rect>
	
	<s:Group width="100%" height="100%" includeIn="hidden">
		<s:Button id="showBtn" label="+" width="100%" height="100%" click="{this.show(event);}" />
	</s:Group>
	
	<s:VGroup width="100%" height="100%" includeIn="normal">
		<pdml:PdmlStack width="100%" height="100%" dataProvider="{this.pdmlStack}" />	
		<s:Button id="hideBtn" width="100%" label="-" click="{this.hide(event)}" />
		<modules:ModulesPool width="100%" height="100%" modulesList="{this.modulesList}" />
	</s:VGroup>
	
</s:WindowedApplication>
